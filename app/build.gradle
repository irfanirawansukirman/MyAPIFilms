apply plugin: 'com.android.application'
apply plugin: 'com.neenbedankt.android-apt'
apply from: '../config/quality/quality.gradle'

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.1"

    defaultConfig {
        applicationId "com.example.yen.imdb"
        minSdkVersion 14
        targetSdkVersion 25
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    productFlavors {
        def baseURL = "\"http://api.myapifilms.com/imdb/\""
        def timeoutInSec = "10"

        dev {
            applicationIdSuffix ".dev"

            timeoutInSec = "12"

            buildConfigField "String", "BASE_URL", baseURL
            buildConfigField "int", "TIMEOUT_IN_SEC", timeoutInSec
        }

        prod {
            applicationIdSuffix ".prod"

            buildConfigField "String", "BASE_URL", baseURL
            buildConfigField "int", "TIMEOUT_IN_SEC", timeoutInSec
        }
    }

    buildTypes {
        debug {}
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    lintOptions {
        warningsAsErrors false
        checkReleaseBuilds true
        abortOnError true
        lintConfig file("$project.rootDir/config/quality/lint/lint.xml")

        disable 'GoogleAppIndexingWarning' // App does not have deep linking.
        disable 'InvalidPackage' // Okio references java.nio that does not presented in Android SDK.
    }

    packagingOptions {
        exclude('META-INF/LICENSE')
        exclude('META-INF/NOTICE')
    }
}

dependencies {
    final BUTTERKNIFE_VERSION = '7.0.1'

    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile "com.android.support:appcompat-v7:$SUPPORT_LIBRARY_VERSION"
    compile "com.android.support:design:$SUPPORT_LIBRARY_VERSION"
    compile "com.android.support:cardview-v7:$SUPPORT_LIBRARY_VERSION"
    compile "com.android.support:recyclerview-v7:$SUPPORT_LIBRARY_VERSION"
    compile "com.google.dagger:dagger:$DAGGER2_VERSION"
    apt "com.google.dagger:dagger-compiler:$DAGGER2_VERSION"
    compile "com.squareup.retrofit2:converter-jackson:$RETROFIT2_VERSION"
    compile ("com.squareup.retrofit2:retrofit:$RETROFIT2_VERSION") {
        exclude module: 'okhttp'
    }
    compile "com.squareup.okhttp3:okhttp:$OKHTTP_VERSION"
    compile "com.squareup.picasso:picasso:$PICASSO_VERSION"
    compile 'com.jakewharton.picasso:picasso2-okhttp3-downloader:1.0.2'
    compile "com.jakewharton:butterknife:$BUTTERKNIFE_VERSION"
    debugCompile "com.squareup.leakcanary:leakcanary-android:$LEAKCANARY_VERSION"
    releaseCompile "com.squareup.leakcanary:leakcanary-android-no-op:$LEAKCANARY_VERSION"
    // Unit test
    testCompile "com.squareup.leakcanary:leakcanary-android-no-op:$LEAKCANARY_VERSION"
    testCompile 'junit:junit:4.12'
    testCompile 'org.robolectric:robolectric:3.2'
    testCompile 'org.mockito:mockito-core:1.10.19'
    // Quality
    compile 'com.google.code.findbugs:annotations:3.0.0'
}